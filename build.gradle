buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: "com.jfrog.bintray"

version = '0.1.0'
group = 'com.github.lookout'
description = 'Gradle plugin for building a prim and proper Lookout service artifact'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'


// Dependency management
////////////////////////
repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    /* Git Gradle plugin (base components) */
    compile 'org.ajoberstar:gradle-git:1.1.0+'


    testCompile('org.spockframework:spock-core:1.0-groovy-2.3') {
        exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib-nodep:2.2.+'
}
////////////////////////


// Tasks
////////
test {
    testLogging {
        /* we want more test failure information, see:
         *  <http://mrhaki.blogspot.com/2013/05/gradle-goodness-show-more-information.html>
         */
        exceptionFormat = 'full'
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}
/* Whenever we're building an artifact, we should run the tests */
assemble.dependsOn check
////////



// Release/publication
//////////////////////
/* Add the sources jar to the list of artifacts to publish */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives groovydocJar
}
//////////////////////
